// weak formulation of the PDEs, implemented in FreeFEM varf format

IFMACRO(!idpWeak)
macro idpWeak 1 // prevent multiple include statements

// all weak forms are in a format that the right hand side of the equation is not moved to
// left hand side, i.e. the right hand side term are also mentioed in the weak form without
// changing the sign (which is the normal way of defining weak form in FreeFEM). this makes
// creating A and b (Ax=b) easy to implement. other approach could be defining separate varf
// statements for right and left hand sides of the weak form.

// parallelization is accomplished by first decomposing mesh labels (done previously at mesh
// generation step) and then integrating equations over each process label only. as a result,
// each MPI process performs integration on its own part of the mesh.

// weak form of Mg ion concentration PDE
varf magnes(Mg, v1) =
int3d(Mesh,mpirank,qfV=qfV1lump,optimize=0)(Mg/dt*v1) + int3d(Mesh,mpirank,qfV=qfV1lump,optimize=0)(Mgold/dt*v1)
+ int3d(Mesh,mpirank)( DeMg*(dx(Mg)*dx(v1) + dy(Mg)*dy(v1) + dz(Mg)*dz(v1)) )
- int3d(Mesh,mpirank,qfV=qfV1lump)((phi <= 0)*k1*Mg*F/Fmax*v1)
+ int3d(Mesh,mpirank,qfV=qfV1lump)((phi <= 0)*k1*Mg*v1)
+ int3d(Mesh,mpirank,qfV=qfV1lump)((phi <= 0)*k2*F*Cl*Cl*v1)
+ int3d(Mesh,mpirank,qfV=qfV1lump)(scaffold*TGV*Mg*v1)
+ int3d(Mesh,mpirank,qfV=qfV1lump)(scaffold*TGV*Mgbc*v1)
;

// weak form of protective film concentration PDE
varf film(F, v2) =
int3d(Mesh,mpirank,qfV=qfV1lump)(F*v2/dt) + int3d(Mesh,mpirank,qfV=qfV1lump, optimize=0)(Fold*v2/dt)
+ int3d(Mesh,mpirank,qfV=qfV1lump)((phi <= 0)*k1*Mg*F/Fmax*v2)
+ int3d(Mesh,mpirank,qfV=qfV1lump)((phi <= 0)*k1*Mg*v2)
+ int3d(Mesh,mpirank,qfV=qfV1lump)((phi <= 0)*k2*F*Cl*Cl*v2)
;

// weak form of chloride ion concentration PDE
varf chloride(Cl, v3) =
int3d(Mesh,mpirank,qfV=qfV1lump,optimize=0)(Cl*v3/dt) + int3d(Mesh,mpirank,qfV=qfV1lump,optimize=0)(Clold*v3/dt)
+ int3d(Mesh,mpirank)( DeCl*(dx(Cl)*dx(v3) + dy(Cl)*dy(v3) + dz(Cl)*dz(v3)) )
;

// weak form of the level set PDE
varf shrink(phi, vp) =
int3d(Mesh,mpirank,optimize=0)(phi*vp/dt) + int3d(Mesh,mpirank,optimize=0)(phiold*vp/dt)
+ int3d(Mesh,mpirank,optimize=0)(v*normgradphi*vp)
;

ENDIFMACRO
